;###############
; CONSTANTES   #
;###############
(defconst aldeanos-feudal 24)


;#############
; RULES      #
;#############

; ENTRENA ALDEANOS
(defrule
    (current-age == dark-age)
    (can-train villager)
    (or
        (civilian-population <= aldeanos-feudal)
        (or
            (and
                (not (can-research ri-loom))
                (not (research-completed ri-loom))
            )
            (and
                (not (can-research feudal-age))
                (not (research-completed feudal-age))
            )
        )
    )
    =>
    (train villager)
)


; CONTRUYE CASAS
(defrule
    (current-age == dark-age)
    (housing-headroom < 4)
    (population-headroom > 3)
    (can-build house)
    =>
    (build house)
)


; CONSTRUYE GRANJAS
(defrule
    (current-age == dark-age)
    (building-type-count-total lumber-camp > 0)
    (unit-type-count-total villager > 15)
    (idle-farm-count < 1)
    (can-build farm)
    =>
    (build farm)
)


; DESARROLLA TELAR
#load-if-not-defined CHINESE-CIV
(defrule
    (current-age == dark-age)
    (civilian-population > aldeanos-feudal)
    (can-research ri-loom)
    =>
    (research ri-loom)
)
#else
(defrule
    (current-age == dark-age)
    (can-research ri-loom)
    =>
    (research ri-loom)
)
#end-if


; PASA A FEUDAL
(defrule
    (current-age == dark-age)
    (civilian-population >= aldeanos-feudal)
    (can-research feudal-age)
    (research-completed ri-loom)
    =>
    (research feudal-age)
)


; CONSTRUCCIONES MOLINO Y MADERERO
#load-if-not-defined HUN-CIV
(defrule
    (current-age == dark-age)
    (unit-type-count villager > 6)
    (building-type-count-total house > 0)
    (resource-found wood)
    (can-build lumber-camp)
    =>
    (build lumber-camp)
    (disable-self)
)


(defrule
    (current-age == dark-age)
    (unit-type-count villager > 6)
    (building-type-count-total house > 0)
    (resource-found food)
    (can-build mill)
    =>
    (build mill)
    (disable-self)
)


#else
(defrule
    (current-age == dark-age)
    (unit-type-count villager > 6)
    (resource-found food)
    (can-build mill)
    =>
    (build mill)
    (disable-self)
)


(defrule
    (current-age == dark-age)
    (unit-type-count villager > 6)
    (resource-found wood)
    (can-build lumber-camp)
    =>
    (build lumber-camp)
    (disable-self)
)
#end-if


;##########################
; NÚMEROS ESTRATÉGICOS    #
;##########################

; ALDEANOS
(defrule
    (current-age == dark-age)
    (civilian-population <= 6)
    =>
    (set-strategic-number sn-percent-civilian-explorers 0)
    (set-strategic-number sn-percent-civilian-gatherers 50)
    (set-strategic-number sn-percent-civilian-builders 50)
    (set-strategic-number sn-food-gatherer-percentage 100)
    (set-strategic-number sn-wood-gatherer-percentage 0)
    (set-strategic-number sn-stone-gatherer-percentage 0)
    (set-strategic-number sn-gold-gatherer-percentage 0)
)


(defrule
    (current-age == dark-age)
    (civilian-population > 6)
    =>
    (set-strategic-number sn-percent-civilian-explorers 0)
    (set-strategic-number sn-percent-civilian-gatherers 50)
    (set-strategic-number sn-percent-civilian-builders 50)
    (set-strategic-number sn-food-gatherer-percentage 70)
    (set-strategic-number sn-wood-gatherer-percentage 30)
    (set-strategic-number sn-stone-gatherer-percentage 0)
    (set-strategic-number sn-gold-gatherer-percentage 0)
)

; EXPLORADORES
(defrule
    (current-age == dark-age)
    =>
    (set-strategic-number sn-total-number-explorers 1)
    (set-strategic-number sn-initial-exploration-required 0)
    (set-strategic-number sn-number-explore-groups 1)
)